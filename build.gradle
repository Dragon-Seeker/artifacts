buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'

apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "artifacts.refmap.json"
}

version = '1.16.5-2.10.4'
group = 'artifacts'
archivesBaseName = 'Artifacts'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'official', version: '1.16.5'

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            mods {
                artifacts {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            mods {
                artifacts {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'artifacts', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            environment 'target', 'fmluserdevdata'
            mods {
                artifacts {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets {
    main.resources.srcDirs += 'src/generated/resources'
}

repositories {
    maven {
        name "curios"
        url "https://maven.theillusivec4.top/"
    }
    maven {
        name "blamejared"
        url "https://maven.blamejared.com/"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        name "jei"
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name "expandability"
        url "https://maven.florens.be"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.24'

    annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'

    runtimeOnly fg.deobf("curse.maven:curious-armor-stands-398784:3288589")
    runtimeOnly fg.deobf("curse.maven:configured-457570:3266880")

    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.16.5-4.0.5.1:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.16.5-4.0.5.1")

    compileOnly fg.deobf("mezz.jei:jei-1.16.4:7.6.0.62:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.16.4:7.6.0.62")

    implementation fg.deobf("be.florens:expandability-forge:2.0.1")
}

mixin {
    add sourceSets.main, 'mixins.artifacts.refmap.json'
    config 'mixins.artifacts.json'

    debug.verbose = true
    debug.export = true
}

jar {
    manifest {
        attributes([
            "Specification-Title": "artifacts",
            "Specification-Vendor": "ochotonida",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor": "ochotonida",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}